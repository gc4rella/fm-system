/*
 * Copyright (c) 2016 Open Baton (http://www.openbaton.org)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    debianDistribution = (applicationRelease) ? "release" : "snapshot"
    debianApplicationVersionFull = "${applicationVersionFull}".replaceAll("-", ".")
    openbatonInstallDir = "/opt/openbaton"
}

task makedeb(type: Deb, dependsOn: [jar, build]) { 
    packageName = (applicationRelease) ? "openbaton-fms" : "openbaton-fms-nightly"
    version = debianApplicationVersionFull
    //release = 1 
    priority = 'optional'
    arch = 'all'
    maintainer = 'Open Baton Team <dev@openbaton.org>'
    vendor = 'Open Source (Implemented by Fraunhofer FOKUS and TUB)'
    license = 'Apache 2.0 License'
    url = 'http://openbaton.github.io/index.html'
    summary = 'Open Baton Fault Management System (FMS)'
    packageDescription = 'The Fault Management System (FMS) is a component of the Open Baton project which is an ETSI compliant implementation of a NFV Management and Orchestration (MANO) System'

    preInstall file('gradle/gradle/scripts/debian/preinst')
    postInstall file('gradle/gradle/scripts/debian/postinst')
    preUninstall file('gradle/gradle/scripts/debian/prerm')
    postUninstall file('gradle/gradle/scripts/debian/postrm')

    requires('openjdk-7-jre')
    requires('rabbitmq-server')
    requires('wget')
    //requires('openbaton-plugins-zabbix')

    // Copy files from "from path" to "into path"
    from('etc/fms.properties') 			                            { into '/etc/openbaton/' }                 // properties file
    from(jar.outputs.files) 				                        { into '/usr/lib/openbaton/' }             // jar file
    from('gradle/gradle/scripts/openbaton-fms') 	                { into '/usr/bin/'}                        // executable to run the FMS
    from('gradle/gradle/scripts/openbaton-fms') 	                { into '/etc/init.d/'}                     // executable to run the FMs as a service (SysV)
    from('gradle/gradle/scripts/openbaton-fms.conf') 	            { into '/etc/init/'}                       // conf file to run the FMS as a service (Upstart)
    from('gradle/gradle/scripts/systemd/openbaton-fms.service') 	{ into '/usr/lib/systemd/system/'}         // executable to run the FMS as a service (Systemd)
    from('gradle/gradle/scripts/systemd/fms.env') 	                { into '/usr/lib/openbaton/systemd/'}      // environment file to run the FMS as a service (Systemd)
    from('gradle/gradle/scripts/systemd/fms.preexecstart') 	        { into '/usr/lib/openbaton/systemd/'}      // preexecstart file to run the FMS as a service (Systemd)
}
